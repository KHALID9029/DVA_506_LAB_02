package project
public

with SEI;
	
--Type
system Complete
end Complete;

--Implementation
system implementation Complete.ACC_system
	subcomponents --To list the subcomponents of the system
		this_speed_sensor:device sensor.speed;
		this_throttle: device actuator.speed;
		this_speed_control: process control.speed;
		this_interface_unit:device interface.pilot;
		this_processor: processor Real_Time.one_GHz;
    	this_bus: bus Marine.Standard;
    	this_ram: memory RAM.Standard;
    	
    	-- New Component for distance control (Task 2)
    	this_distance_sensor: device sensor.distance;
    	this_brake: device actuator.brake;
    	this_distance_interface: device distanceinterface.distance;
    	
	connections --List all the port and bus connection
		DC1: port this_speed_sensor.sensor_data -> this_speed_control.sensor_data;
		DC2: port this_speed_control.command_data -> this_throttle.cmd;
		DC3: port this_interface_unit.set_speed -> this_speed_control.set_speed;
		EC4: port this_interface_unit.disengage -> this_speed_control.disengage;
		BAC1: bus access this_bus <-> this_speed_sensor.bus_access;
    	BAC2: bus access this_bus <-> this_processor.bus_access;
    	BAC3: bus access this_bus <-> this_throttle.bus_access;
    	BAC4: bus access this_bus <-> this_interface_unit.bus_access;
    	BAC5: bus access this_bus <-> this_ram.bus_access;
    	
    	-- For distance control (Task 2)
    	DC5: port this_distance_sensor.sensor_data -> this_speed_control.distance_data;
    	DC6: port this_speed_control.distance_command_data -> this_brake.cmd;
    	DC7: port this_distance_interface.set_distance -> this_speed_control.set_distance;
    	BAC6: bus access this_bus <-> this_distance_sensor.bus_access;
    	BAC7: bus access this_bus <-> this_brake.bus_access;
    	BAC8: bus access this_bus <-> this_distance_interface.bus_access;
    	
	flows --Task 6
		-- End to end flows for speed control
		speed_end_to_end: end to end flow
			this_interface_unit.speed_flow_src -> DC3 ->
			this_speed_control.speed_control_flow_path -> DC2 ->
			this_throttle.flow_sink {Latency => 100 ms .. 100 ms;};
				
		-- End to end flows for distance control
		distance_end_to_end: end to end flow
			this_distance_interface.distance_flow_src -> DC7 ->
			this_speed_control.distance_control_flow_path -> DC6 ->
			this_brake.flow_sink {Latency => 100 ms .. 100 ms;};
			
	properties		
		-- Task 4: Reference_Processor
		Reference_Processor => classifier(Real_Time.one_GHz);
		Scheduling_Protocol => (RMS) applies to this_processor;
		
		-- Binding software to appropriate hardware
  		Actual_Processor_Binding => (reference(this_processor))
    applies to this_speed_control.this_speed_control_laws_thread;
  		Actual_Processor_Binding => (reference(this_processor))
    applies to this_speed_control.this_speed_data_thread;
    	Actual_Processor_Binding => (reference(this_processor))
	applies to this_speed_control.this_distance_data_thread;
  		Actual_Memory_Binding => (reference(this_ram))
    applies to this_speed_control;
  		
   		-- Task 5: Bandwidth budget for connections
  		SEI::BandWidthBudget => 10.0 bitsps applies to 
  			DC1, DC2, DC3, DC5, DC6, DC7, EC4, 
  			BAC1, BAC2, BAC3, BAC4, BAC5, BAC6, BAC7, BAC8;
end Complete.ACC_system;


--Definition of Components 1) Type 2) Implementation
--Sensor
device sensor
	features 
		sensor_data: out data port;
		bus_access: requires bus access Marine.Standard; -- Bus access connection
		
		flows --Task 6
			flow_src: flow source sensor_data {Latency => 1 ms .. 2 ms;};
			
	properties 
		--task 4, MIPS for each device
		SEI::MIPSBudget => 0.2 MIPS;
		--Task 5: Period for each device
		Period => 10 ms;
end sensor;
	
device implementation sensor.speed
end sensor.speed;

--New Component for distance control (Task 2)
device implementation sensor.distance
end sensor.distance;
	
--Interface
device interface
	features
		set_speed: out data port;
		disengage: out event port;
		bus_access: requires bus access Marine.Standard;
		
	flows --Task 6
		speed_flow_src: flow source set_speed {Latency => 4 ms .. 4 ms;};
				
	properties 
		--task 4, MIPS for each device
		SEI::MIPSBudget => 0.2 MIPS;
		--Task 5: Period for each device
		Period => 10 ms;
end interface;
	
device implementation interface.pilot
end interface.pilot;

--New Component for distance control (Task 2)
device distanceinterface
	features
		set_distance: out data port; --new
		bus_access: requires bus access Marine.Standard;
		
		flows --Task 6
			distance_flow_src: flow source set_distance {Latency => 4 ms .. 4 ms;};
			
	properties 
		--task 4, MIPS for each device
		SEI::MIPSBudget => 0.2 MIPS;
		--Task 5: Period for each device
		Period => 10 ms;
end distanceinterface;

device implementation distanceinterface.distance
end distanceinterface.distance;

--Defining a process
process control
	features
		command_data: out data port;
		sensor_data: in data port;
		set_speed: in data port;
		distance_data: in data port;
		set_distance: in data port;
		distance_command_data: out data port;
		disengage: in event port;
		
		flows --Task 6
		speed_control_flow_path: flow path set_speed -> command_data 
		    {Latency => 10 ms .. 20 ms;};
		distance_control_flow_path: flow path set_distance -> distance_command_data 
		    {Latency => 10 ms .. 20 ms;};
end control;

process implementation control.speed
	subcomponents
		this_speed_data_thread: thread read_data.speed;
		this_distance_data_thread: thread read_data.distance; --new
    	this_speed_control_laws_thread: thread control_laws.speed;
	connections
		--Speed data flow
    	DC1: port sensor_data -> this_speed_data_thread.sensor_data;
    	DC2: port this_speed_data_thread.proc_data ->
      this_speed_control_laws_thread.proc_data;
    	DC3: port this_speed_control_laws_thread.cmd -> command_data;
    	EC1: port disengage -> this_speed_control_laws_thread.disengage;
    	DC4: port set_speed -> this_speed_control_laws_thread.set_speed;
    	
    	--distance data flow
    	DC5: port distance_data -> this_distance_data_thread.sensor_data;
		DC6: port this_distance_data_thread.proc_data -> 
		this_speed_control_laws_thread.distance_proc_data;
		DC7: port this_speed_control_laws_thread.distance_cmd -> 
		distance_command_data;
		DC8: port set_distance -> this_speed_control_laws_thread.set_distance;
		
		flows --Task 6
		speed_control_flow_path: flow path 
			set_speed -> DC4 -> 
			this_speed_control_laws_thread.speed_flow_path -> 
			DC3 -> command_data;
		distance_control_flow_path: flow path 
			set_distance -> DC8 -> 
			this_speed_control_laws_thread.distance_flow_path -> 
			DC7 -> distance_command_data;	
end control.speed;
	
	
--Actuator
device actuator
	features
		cmd:in data port;
		bus_access: requires bus access Marine.Standard;
		
	flows --Task 6
		flow_sink: flow sink cmd {Latency => 4 ms .. 4 ms;};
		
	properties 
		--task 4, MIPS for each device
		SEI::MIPSBudget => 0.2 MIPS;
		--Task 5: Period for each device
		Period => 10 ms;
end actuator;

device implementation actuator.speed
end actuator.speed;

--New Component for distance control (Task 2)
device implementation actuator.brake
end actuator.brake;

--Defining Thread
thread read_data
  	features
    	sensor_data: in data port;
    	proc_data: out data port;
  	properties
    	Dispatch_Protocol => Periodic;
    	Compute_Execution_Time => 2 ms .. 4 ms;
    	Period => 60 ms;
		SEI::MIPSBudget => 0.4 MIPS; --task 4, MIPS for each read thread
end read_data;

thread implementation read_data.speed
end read_data.speed;

--New thread for distance control (Task 2)
thread read_distance_data
	features
		sensor_data: in data port;
		proc_data: out data port;
	properties
		Dispatch_Protocol => Periodic;
		Compute_Execution_Time => 4 ms .. 8 ms; --2X of speed function
		Period => 60 ms;
		SEI::MIPSBudget => 0.4 MIPS; --task 4, MIPS for each read thread
end read_distance_data;

thread implementation read_data.distance

end read_data.distance;

thread control_laws
  	features
    	proc_data: in data port;
    	cmd: out data port;
    	set_speed: in data port;
    	--new
    	distance_proc_data: in data port;
    	distance_cmd: out data port;
    	set_distance: in data port;
  		disengage: in event port;
  	
  	flows --Task 6
		speed_flow_path: flow path set_speed -> cmd {Latency => 3 ms .. 6 ms;};
		distance_flow_path: flow path set_distance -> distance_cmd {Latency => 3 ms .. 6 ms;};
		
		properties
			Dispatch_Protocol => Periodic;
    		Compute_Execution_Time => 3 ms .. 6 ms;
    		Period => 60 ms;
    		SEI::MIPSBudget => 4.0 MIPS; --task 4, MIPS for control laws thread
end control_laws;

thread implementation control_laws.speed
end control_laws.speed;

-- Defining Processor
processor Real_Time
	features
		bus_access: requires bus access Marine.Standard; 
end Real_Time;

processor implementation Real_Time.one_GHz
		
	properties 
		--task 4  MIPS capacity
		SEI::MIPSCapacity => 10.0 MIPS;
end Real_Time.one_GHz;

-- Defining memory
memory RAM
  	features
    	bus_access: requires bus access Marine.Standard;
end RAM;

memory implementation RAM.Standard
end RAM.Standard;

-- Defining bus (Physical connection)
bus Marine
end Marine;

bus implementation Marine.Standard
	properties
		-- Task 5: Bus bandwidth
		SEI::BandWidthCapacity => 10000.0 bitsps;
		-- Task 5: Bus fixed transmission time
		Transmission_Time => [ Fixed => 4ms .. 8ms; ];
end Marine.Standard;
end project;
